global project "/Users/liran/Desktop/CRN"
cap mkdir "${project}/results_250325"
cd "${project}/results_250325"
use "${project}/clean_data/clean.dta",clear
svyset psu9621 [pw = poolwt], strata(stra9621) vce(linearized) singleunit(missing)

gen domain=(hf==1&age>=18&acceli42==1)

foreach num in 1 2 {
gen domain_agecat`num'=(agecat2==`num'&domain==1)
}

/****************************************************************************
* HF patients who got delayed medications vs who never got the medications. * 
****************************************************************************/
tab afford_PM delay_PM if domain==1 

g num_group=. 
replace num_group=1 if domain==1 & afford_PM==1 & delay_PM==1 
replace num_group=2 if domain==1 & afford_PM==1 & delay_PM==0 
replace num_group=3 if domain==1 & afford_PM==0 & delay_PM==1 

lab def num_group 1 "both" ///
2 "didn't get only" ///
3 "delay only" ,replace 

lab values num_group num_group

tab num_group if domain==1 & (afford_PM==1 | delay_PM==1),m 

local comorbidities hypertension diabetes arthritis copd ascvd asthma anemia physical cancer smoke comor

g domain_crn= (domain==1 & (afford_PM==1 | delay_PM==1))

foreach var in agecat2 sex race poverty inscov insurance region edu `comorbidities'{
estpost svy , sub(domain_crn):  tab `var' num_group, column ci percent nototal
estadd matrix ci_u=e(ub)
estadd matrix ci_l=e(lb)
est sto res_rr_`var'
}

*results
cap erase table_rr1.rtf
local comorbidities hypertension diabetes arthritis copd ascvd asthma anemia physical cancer  smoke comor
foreach var in agecat sex race poverty inscov insurance region edu `comorbidities'{
esttab res_rr_`var' using "table_rr1.rtf",b(2) ci nostar onecell scalars(p_Pear) sfmt(4) unstack nonotes label nomtitle nonumbers append
}

************************
* Results of 2012-2017 * 
************************
cd "${project}/results_250325"
use "${project}/clean_data/clean.dta",clear
keep if year>=2012 & year<=2017
svyset psu9621 [pw = poolwt], strata(stra9621) vce(linearized) singleunit(missing)

gen domain=(hf==1&age>=18&acceli42==1)

foreach num in 1 2 {
gen domain_agecat`num'=(agecat2==`num'&domain==1)
}

local comorbidities hypertension diabetes arthritis copd ascvd asthma anemia physical cancer smoke comor 

foreach num in 1 2 {
foreach crncat in afford_PM delay_PM crn { 
foreach var in sex race poverty inscov insurance region edu `comorbidities'{
estpost svy , sub(domain_agecat`num'):  tab `var' `crncat', row ci percent nototal
estadd matrix ci_u=e(ub)
estadd matrix ci_l=e(lb)
est sto rr_`crncat'_`var'`num'
}
}
}

cap erase "table_rr2.rtf" 
local comorbidities hypertension diabetes arthritis copd ascvd asthma anemia physical cancer smoke edu comor
foreach var in sex race poverty inscov insurance region `comorbidities'{
esttab rr_crn_`var'1 rr_afford_PM_`var'1 rr_delay_PM_`var'1 rr_crn_`var'2 rr_afford_PM_`var'2 rr_delay_PM_`var'2 using "table_rr2.rtf",b(2) ci nostar nonotes label mtitle nonumbers unstack sfmt(fmt[%20.2f]) append
} 

*****************************************
* fig of polynomial logistic regression * 
*****************************************
cap mkdir "${project}/figs"
cd "${project}/figs"
use "${project}/clean_data/clean.dta",clear
svyset psu9621 [pw = poolwt], strata(stra9621) vce(linearized) singleunit(missing)
gen domain=(hf==1&age>=18&acceli42==1)
foreach num in 1 2 {
gen domain_`num'=(domain==1&agecat2==`num')
}
set scheme s1color, perm

g num_group=0  if domain==1
replace num_group=1 if domain==1 & afford_PM==1 & delay_PM==1 
replace num_group=2 if domain==1 & afford_PM==1 & delay_PM==0 
replace num_group=3 if domain==1 & afford_PM==0 & delay_PM==1 

lab def num_group 1 "both" ///
2 "didn't get only" ///
3 "delay only" ,replace 

lab values num_group num_group

svy,sub(domain): mlogit num_group ib3.agecat i.sex ib2.race i.edu2 i.lowinc ib2.inscov ib1.region i.comor, coeflegend
